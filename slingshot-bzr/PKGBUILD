# Maintainer: Ner0

pkgname=slingshot-bzr
pkgver=297
pkgrel=1
pkgdesc="A lightweight and stylish app launcher from elementary"
arch=('i686' 'x86_64')
url="https://www.launchpad.net/slingshot"
license=('GPL3')
groups=('pantheon')
depends=('glib2' 'libgee' 'gnome-menus2' 'gtk3' 'granite-bzr' 'dconf' 'glibc' 'libwnck3'
         'cairo' 'hicolor-icon-theme' 'gtk-update-icon-cache' 'libzeitgeist')
makedepends=('bzr' 'cmake' 'vala' 'pkg-config')
optdepends=('wingpanel-bzr: An elementary panel. [AUR]'
            'zeitgeist: zeitgeist support.')
install=$pkgname.install

_bzrtrunk=lp:slingshot
_bzrmod=new-slingshot

build() {
  msg "Connecting to Bazaar server...."

  if [[ -d "$_bzrmod" ]]; then
    cd "$_bzrmod" && bzr pull "$_bzrtrunk" -r $pkgver && cd ..
    msg "The local files are updated."
  else
    bzr branch "$_bzrtrunk" "$_bzrmod" -r $pkgver
  fi

  msg "BZR checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$_bzrmod-build"
  cp -rf "$_bzrmod" "$_bzrmod-build"
  cd "$_bzrmod-build"

  sed -i 's/unity;//' CMakeLists.txt
  sed -i '/Unity/d' src/Backend/App.vala

  rm -rf build/
  mkdir build
  cd build
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DGSETTINGS_COMPILE=OFF -DCMAKE_EXE_LINKER_FLAGS="$CFLAGS -lm"
  make ${MAKEFLAGS}
}

package() {
  cd "$_bzrmod-build/build"
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="$pkgdir/" install
}
