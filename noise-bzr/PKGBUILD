# Maintainer: Ner0

pkgname=noise-bzr
pkgver=1176
pkgrel=1
pkgdesc="The official audio player of elementary OS."
arch=('i686' 'x86_64')
url="https://launchpad.net/noise"
license=('GPL3')
depends=('glib2' 'gtk3' 'gstreamer0.10' 'gstreamer0.10-base' 'taglib' 'sqlheavy'
         'libxml2' 'libnotify' 'libsoup' 'json-glib' 'granite-bzr' 'libgpod'
         'libpeas' 'desktop-file-utils' 'dconf' 'hicolor-icon-theme' 'libdbusmenu' 'libindicate')
makedepends=('bzr' 'cmake' 'vala' 'pkg-config')
optdepends=('gstreamer0.10-plugins: Plugins for various audio formats')
install=$pkgname.install

_bzrtrunk=lp:noise
_bzrmod=noise

build() {
  msg "Connecting to Bazaar server...."

  if [[ -d "$_bzrmod" ]]; then
    cd "$_bzrmod" && bzr pull "$_bzrtrunk" -r $pkgver && cd ..
    msg "The local files are updated."
  else
    bzr branch "$_bzrtrunk" "$_bzrmod" -r $pkgver
  fi

  msg "BZR checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$_bzrmod-build"
  cp -r "$_bzrmod" "$_bzrmod-build"
  cd "$_bzrmod-build"
  rm -rf build/
  mkdir build
  cd build
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DGSETTINGS_COMPILE=OFF -DICON_UPDATE=OFF -DCMAKE_EXE_LINKER_FLAGS="$CFLAGS -lm"
  make
}

package() {
  cd "$_bzrmod-build/build"
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="$pkgdir/" install
}
